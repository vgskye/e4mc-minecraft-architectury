plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.modmuss50.mod-publish-plugin" version "0.3.0"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    shadowCommon(implementation("com.github.vgskye.netty-incubator-codec-quic:netty-incubator-codec-classes-quic:${rootProject.quic_version}") {
        exclude(group: "io.netty")
    })
    shadowCommon('folk.sisby:kaleido-config:0.3.1+1.3.1')
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"

    relocate 'com.electronwill.nightconfig', 'link.e4mc.shadow.nightconfig'
    relocate 'folk.sisby.kaleido', 'link.e4mc.shadow.kaleido'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenNeoForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        maven {
            url "https://sg.storage.bunnycdn.com/skyeven"
            credentials(HttpHeaderCredentials) {
                name = "AccessKey"
                value = property("skyevenToken")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    type = STABLE
    changelog = getLatestChangelog()
    modLoaders.add("neoforge")

    version = project.version + "-neoforge"
    displayName = "[NeoForge] $project.version"

    // Uncomment your desired platform(s)
    curseforge {
        accessToken = project.curseforgeApiToken
        projectId = project.curseforgeId
        minecraftVersions.addAll(
                "1.20.2", "1.20.3", "1.20.4", "1.20.5", "1.20.6",
                "1.21", "1.21.1"
        )
    }

    modrinth {
        accessToken = project.modrinthApiToken
        projectId = project.modrinthId
        minecraftVersions.addAll(
                "1.20.2", "1.20.3", "1.20.4", "1.20.5", "1.20.6",
                "1.21", "1.21.1"
        )
    }
}
